;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;; Extempore's data types library
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; lib-loading config
(sys:load-preload-check 'adt)
(define *xtmlib-adt-loaded* #t)

(impc:aot:suppress-aot-do
 (sys:load "libs/core/std.xtm"))
(impc:aot:insert-sexp '(sys:load "libs/core/std.xtm" 'quiet))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;; fully generalised first and second

(bind-func first:[!a,!b]*
  (lambda (a)
    (tref a 0)))

(bind-func second:[!a,!b]*
  (lambda (a)
    (tref a 1)))

;;;;;;;;;;;;;;;;;;;;
;; points

(bind-type Point <!x,!x>)

(bind-func print:[void,Point*]*
  (lambda (p)
    (if (null? p)
        (begin (printout "<>") void)
        (begin (printout "<"
                         (tref p 0) ","
                         (tref p 1) ">")
               void))))

(bind-func first:[!a,Point:<!a,!a>*]*
  (lambda (a)
    (tref a 0)))

(bind-func second:[!a,Point:<!a,!a>*]*
  (lambda (a)
    (tref a 1)))

;;;;;;;;;;;;;;;;;;;;
;; pairs

(bind-type Pair <!a,!b>)

(bind-func print:[void,Pair*]*
  (lambda (p)
    (if (null? p)
        (begin (printout "<>") void)
        (begin (printout "<" (tref p 0) "," (tref p 1) ">")
               void))))

(bind-func first:[!a,Pair:<!a,!b>*]*
  (lambda (a)
    (tref a 0)))

(bind-func second:[!b,Pair:<!a,!b>*]*
  (lambda (a)
    (tref a 1)))

(bind-func third:[!b,Pair:<!a,!b>*]*
  (lambda (a)
    (tref a 2)))

(bind-func fourth:[!b,Pair:<!a,!b>*]*
  (lambda (a)
    (tref a 3)))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; singly linked lists
;;
(bind-type List <!a,List*>)

(bind-func print:[void,List*]*
  (lambda (xs)
    (if (null? xs)
        (begin (printout "()") void)
        (begin
          (printout "(" (car xs))
          (for-each (lambda (x) (printout " " x)) (cdr xs))
          (printout ")")
          void))))

(bind-func toString:[String*,List*]*
  (lambda (xs)
    (if (null? xs)
        (sprintln "()")
        (let ((ska:String* (sprintout "(" (car xs))))
          (for-each (lambda (xz)
                      (set! ska (sprintout ska " " xz)))
                    (cdr xs))
          (sprintout ska ")")))))


(bind-func cons:[List:<!a,List*>*,!a,List:<!a,List*>*]*
  (lambda (x y)
    (let ((obj (alloc)))
      (tset! obj 0 x)
      (tset! obj 1 y)
      obj)))

(bind-func cons_h:[List:<!a,List*>*,!a,List:<!a,List*>*]*
  (lambda (x y)
    (let ((obj (halloc)))
      (tset! obj 0 x)
      (tset! obj 1 y)
      obj)))

(bind-func car:[!a,List:<!a,List*>*]*
  (lambda (a)
    (tref a 0)))

(bind-func cdr:[List:<!a,List*>*,List:<!a,List*>*]*
  (lambda (a)
    (tref a 1)))

(bind-func set_car:[!a,List:<!a,List*>*,!a]*
  (lambda (a val)
    (tset! a 0 val)))

(bind-func set_cdr:[List:<!a,List*>*,List:<!a,List*>*,,List:<!a,List*>*]*
  (lambda (a val)
    (tset! a 1 val)))

(bind-func length:[i64,List:<!a,List*>*]*
  (lambda (a)
    (if (null? a)
        0
        (+ 1 (length (cdr a))))))

(bind-func copy:[List:<!a,List*>*,List:<!a,List*>*]*
  (lambda (lst)
    (let ((orig lst)
          (cpy null))
      (while (not (null? orig))
        (set! cpy (cons (car orig) cpy))
        (set! orig (cdr orig)))
      (reverse cpy))))

(bind-func copy_h:[List:<!a,List*>*,List:<!a,List*>*]*
  (lambda (lst)
    (let ((orig lst)
          (cpy null))
      (while (not (null? orig))
        (set! cpy (cons_h (car orig) cpy))
        (set! orig (cdr orig)))
      (reverse cpy))))

(bind-func equal:[List:<!a,List*>*,List:<!a,List*>*,List:<!a,List*>*]*
  "returns true if 'equal' returns true for all pairs of elements"
  (lambda (list1 list2)
    (every (lambda (a b) (equal a b)) list1 list2)))

(bind-func nth_tailr:[List:<!a,List*>*,List:<!a,List*>*,i64]*
  (lambda (lst idx)
    (let ((nt (lambdas (l i cnt)
                       (if (or (= cnt i) (> cnt i) (null? l))
                           l
                           (nt (cdr l)
                               i
                               (+ cnt 1))))))
      (let ((r (nt lst idx 0)))
        r))))

(bind-func nth_tail:[List:<!a,List*>*,List:<!a,List*>*,i64]*
  (lambda (lst idx)
    (if (or (<= idx 0) (null? lst))
        lst
        (nth_tail (cdr lst) (- idx 1)))))

(bind-func nthr:[!a,List:<!a,List*>*,i64]*
  (lambda (lst idx)
    (car (nth_tailr lst idx))))

(bind-func nth:[!a,List:<!a,List*>*,i64]*
  (lambda (lst idx)
    (car (nth_tail lst idx))))

(bind-func insert:[List:<!a,List*>*,List:<!a,List*>*,i64,!a]*
  (lambda (lst idx value)
    (if (= idx 0)
        (cons value lst)
        (let ((pre_cell (nth_tail lst (- idx 1)))
              (new_cell (cons value (cdr pre_cell))))
          (tset! pre_cell 1 new_cell)
          lst))))

(bind-func delete:[!a,List:<!a,List*>*,i64]*
  (lambda (lst idx)
    (let ((pre_cell (nth_tail lst (- idx 1)))
          (cell (cdr pre_cell))
          (post_cell (cdr cell)))
      (tset! pre_cell 1 post_cell)
      (car cell))))

(bind-func reverse_inline:[List:<!a,List*>*,List:<!a,List*>*,List:<!a,List*>*]*
  (lambda (old new)
    (if (null? old) new
        (reverse_inline (cdr old) (cons (car old) new)))))

(bind-func reverse:[List:<!a,List*>*,List:<!a,List*>*]*
  (lambda (lst)
    (reverse_inline lst (convert null))))

(bind-func reverse_r:[List:<!a,List*>*,List:<!a,List*>*]*
  (lambda (lst)
    (let ((rev (lambda (old new)
                 (if (null? old)
                     new
                     (rev (cdr old) (cons (car old) new))))))
      (rev lst (convert null)))))

(bind-func map:[List:<!a,List*>*,[!a,!b]*,List:<!b,List*>*]*
  (lambda (func xs)
    (let ((f (lambda (l)
               (if (null? l)
                   null
                   (cons (func (car l)) (f (cdr l)))))))
      (f xs))))

(bind-func map:[List:<!a,List*>*,[!a,!b,!c]*,List:<!b,List*>*,List:<!c,List*>*]*
  (lambda (func xs1 xs2)
    (let ((f (lambda (l1 l2)
               (if (null? l1)
                   null
                   (cons (func (car l1) (car l2))
                         (f (cdr l1) (cdr l2)))))))
      (f xs1 xs2))))

(bind-func map:[List:<!a,List*>*,[!a,!b,!c,!d]*,List:<!b,List*>*,List:<!c,List*>*,List:<!d,List*>*]*
  (lambda (func xs1 xs2 xs3)
    (let ((f (lambda (l1 l2 l3)
               (if (null? l1)
                   null
                   (cons (func (car l1) (car l2) (car l3))
                         (f (cdr l1) (cdr l2) (cdr l3)))))))
      (f xs1 xs2 xs3))))

(bind-func filter:[List:<!a,List*>*,[bool,!a]*,List:<!a,List*>*]*
  (lambda (pred xs)
    (let ((f (lambda (l)
               (if (null? l)
                   null
                   (if (pred (car l))
                       (cons (car l) (f (cdr l)))
                       (f (cdr l)))))))
      (f xs))))

(bind-func index_of:[size_t,!a,List:<!a,List*>*]*
  "return index of first occurrence of element in lst, or -1 if not present"
  (lambda (element lst)
    (let ((f (lambda (l cnt)
               (if (null? l)
                   -1
                   (if (equal element (car l))
                       cnt
                       (f (cdr l) (set! cnt (+ cnt 1))))))))
      (f lst 0))))

(bind-func index_of:[size_t,!a,List:<!a,List*>*,[bool,!a,!a]*]*
  "return index of first occurrence of element in lst, or -1 if not present"
  (lambda (element lst pred)
    (let ((f (lambda (l cnt)
               (if (null? l)
                   -1
                   (if (pred element (car l))
                       cnt
                       (f (cdr l) (set! cnt (+ cnt 1))))))))
      (f lst 0))))

(bind-func member:[List:<!a,List*>*,!a,List:<!a,List*>*]*
  (lambda (element lst)
    (let ((f (lambda (l)
               (if (null? l)
                   null
                   (if (equal element (car l))
                       l
                       (f (cdr l)))))))
      (f lst))))

(bind-func member:[List:<!a,List*>*,!a,List:<!a,List*>*,[bool,!a,!a]*]*
  (lambda (element lst pred)
    (let ((f (lambda (l)
               (if (null? l)
                   null
                   (if (pred element (car l))
                       l
                       (f (cdr l)))))))
      (f lst))))

(bind-func any:[bool,[bool,!a]*,List:<!a,List*>*]*
  (lambda (pred lst)
    (let ((f (lambda (l)
               (if (null? l)
                   #f
                   (if (pred (car l))
                       #t
                       (f (cdr l)))))))
      (f lst))))

(bind-func any:[bool,[bool,!a,!b]*,List:<!a,List*>*,List:<!b,List*>*]*
  (lambda (pred lst1 lst2)
    (let ((f (lambda (l1 l2)
               (if (or (null? l1) (null? l2))
                   #f
                   (if (pred (car l1) (car l2))
                       #t
                       (f (cdr l1) (cdr l2)))))))
      (f lst1 lst2))))

(bind-func every:[bool,[bool,!a]*,List:<!a,List*>*]*
  (lambda (pred lst)
    (let ((f (lambda (l)
               (if (null? l)
                   #t
                   (if (not (pred (car l)))
                       #f
                       (f (cdr l)))))))
      (f lst))))

(bind-func every:[bool,[bool,!a,!b]*,List:<!a,List*>*,List:<!b,List*>*]*
  (lambda (pred lst1 lst2)
    (let ((f (lambda (l1 l2)
               (if (or (null? l1) (null? l2))
                   #t
                   (if (not (pred (car l1) (car l2)))
                       #f
                       (f (cdr l1) (cdr l2)))))))
      (f lst1 lst2))))

(bind-func for-each:[void,[!a,!b]*,List:<!b,List*>*]*
  (lambda (func xs)
    (let ((f (lambda (l)
               (if (null? l) void
                   (begin (func (car l))
                          (f (cdr l)))))))
      (f xs))))

(bind-func for-each:[void,[!a,!b,!c]*,List:<!b,List*>*,List:<!c,List*>*]*
  (lambda (func xs1 xs2)
    (let ((f (lambda (l1 l2)
               (if (null? l1) void
                   (begin (func (car l1) (car l2))
                          (f (cdr l1) (cdr l2)))))))
      (f xs1 xs2))))

(bind-func for-each:[void,[!a,!b,!c,!d]*,List:<!b,List*>*,List:<!c,List*>*,List:<!d,List*>*]*
  (lambda (func xs1 xs2 xs3)
    (let ((f (lambda (l1 l2 l3)
               (if (null? l1) void
                   (begin (func (car l1) (car l2) (car l3))
                          (f (cdr l1) (cdr l2) (cdr l3)))))))
      (f xs1 xs2 xs3))))

(bind-func last:[List:<!a,List*>*,List:<!a,List*>*]*
  (lambda (a)
    (if (null? (cdr a))
        a
        (last (cdr a)))))

(bind-func range:[List:<!a,List*>*,!a]*
  (lambda (end)
    (let ((val (- end (convert 1)))
          (result null))
      (while (>= val (convert 0))
        (set! result (cons val result))
        (set! val (- val (convert 1))))
      result)))

(bind-func range:[List:<!a,List*>*,!a,!a]*
  (lambda (start end)
    (let ((val (- end (convert 1)))
          (result null))
      (while (>= val start)
        (set! result (cons val result))
        (set! val (- val (convert 1))))
      result)))

(bind-func range:[List:<!a,List*>*,!a,!a,!a]*
  (lambda (start end step)
    (if (<= step 0)
        (begin (println "Problem in call to range: step must be positive")
               null)
        (let ((val start)
              (result null))
          (while (< val end)
            (set! result (cons val result))
            (set! val (+ val step)))
          (reverse result)))))

(bind-func append:[List:<!a,List*>*,List:<!a,List*>*,List:<!a,List*>*]*
  (lambda (a b)
    (if (null? a) b
        (begin
          (tset! (last a) 1 b)
          a))))

(bind-func swap:[List:<!a,List*>*,List:<!a,List*>*,i64,i64]*
  ;; swap ith and jth list elements inplace
  (lambda (lst i j)
    (let ((ith_val (nth lst i)))
      (set_car (nth_tail lst i) (nth lst j))
      (set_car (nth_tail lst j) ith_val))
    lst))

;; fold/reduce

(bind-func foldl:[!b,[!b,!b,!a]*,!b,List:<!a,List*>*]*
  (lambda (fn start lst)
    (let ((fold (lambda (l r) ; list result
                  (if (null? l)
                      r
                      (fold (cdr l) (fn r (car l)))))))
      (fold lst start))))

(bind-func foldr:[!b,[!b,!a,!b]*,!b,List:<!a,List*>*]*
  (lambda (fn end lst)
    (let ((fold (lambda (l r) ; list result
                  (if (null? l)
                      r
                      (fold (cdr l) (fn (car l) r))))))
      (fold (reverse lst) end))))

(bind-func take:[List:<!a,List*>*,List:<!a,List*>*,i64]*
  "returns a copy of the first n elements of lst"
  (lambda (lst n)
    (let ((newlst (list))
          (oldlst lst)
          (count 0))
      (while (and (< count n)
                  (not (null? oldlst)))
        (set! newlst (cons (car oldlst) newlst))
        (set! oldlst (cdr oldlst))
        (set! count  (+ count 1)))
      (reverse newlst))))

(bind-func drop:[List:<!a,List*>*,List:<!a,List*>*,i64]*
  "returns a copy of lst sans the first n elements"
  (lambda (lst n)
    (copy (nth_tail lst n))))

(bind-func duplicates:[List:<!a,List*>*,List:<!a,List*>*]*
  (lambda (lst)
    (let ((dupes (list)))
      (while (not (null? lst))
        (if (not (null? (member (car lst) (cdr lst))))
            (set! dupes (cons (car lst) dupes)))
        (set! lst (cdr lst)))
      (reverse dupes))))

(bind-func unique:[List:<!a,List*>*,List:<!a,List*>*]*
  (lambda (lst)
    (let ((unq (list)))
      (while (not (null? lst))
        (if (null? (member (car lst) (cdr lst)))
            (set! unq (cons (car lst) unq)))
        (set! lst (cdr lst)))
      (reverse unq))))

(bind-func intersection:[List:<!a,List*>*,List:<!a,List*>*,List:<!a,List*>*]*
  (lambda (l1 l2)
    (duplicates (append (unique l1) (unique l2)))))

(bind-func union:[List:<!a,List*>*,List:<!a,List*>*,List:<!a,List*>*]*
  (lambda (l1 l2)
    (unique (append l1 l2))))

;; push can't really work without changing the cells around, and can
;; never work for the (push 42 (list)) case, so it's best to not use
;; it for the moment

;; (bind-func push:[void,!a,List:<!a,List*>*]*
;;   "add element to head of list (modifies list in-place)"
;;   (lambda (element lst)
;;     (set! lst (cons element lst))
;;     void))

(bind-func pop:[!a,List:<!a,List*>*]*
  "pop first element from list (modifies list in-place)"
  (lambda (lst)
    (if (null? lst)
        (convert null)
        (let ((element (car lst)))
          (set! lst (cdr lst))
          element))))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;; a simple 'assoc' dictionary
;;
;; the "dictionary" is any List of Pairs
;; where the pair is any <i8*,!a>
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(bind-func dict_get:[!v,List:<Pair:<String*,!v>*,List*>*,String*]*
  "find value for key in dict (which must be a list of any <i8*,!v>)"
  (lambda (dict key)
    (if (null? dict)
        (convert null)
        (if (string_cmp (first (car dict)) key)
            (second (car dict))
            (dict_get (cdr dict) key)))))

(bind-func dict_set:[!v,List:<Pair:<String*,!v>*,List*>*,String*,!v]*
  "set (or overwrite) value for key in dict (returns the old value)"
  (lambda (dict key value)
    (if (null? dict)
        (convert null)
        (if (string_cmp (first (car dict)) key)
            (let ((oldval (second (car dict))))
              (tset! (car dict) 1 value)
              oldval)
            (if (null? (cdr dict))
                (begin (append dict (list (Pair key value)))
                       (convert null))
                (dict_set (cdr dict) key value))))))

(bind-func dict_update:[!v,List:<Pair:<String*,!v>*,List*>*,String*,[!v,!v]*]*
  "set (or update) value for key in dict according to update_fn (returns the old value)"
  (lambda (dict key update_fn)
    (if (null? dict)
        (convert null)
        (if (string_cmp (first (car dict)) key)
            (let ((oldval (second (car dict)))
                  (newval (update_fn oldval)))
              ;; if update_fn returns null, do nothing. otherwise
              ;; update the value for key
              (tset! (car dict) 1 newval)
              oldval)
            (if (null? (cdr dict))
                (begin (append dict (list (Pair key (update_fn (convert null)))))
                       (convert null))
                (dict_update (cdr dict) key update_fn))))))

;;;;;;;;;;;
;; TREES ;;
;;;;;;;;;;;

;; Binary Tree

(bind-type BTree <!v,BTree*,BTree*>)

(bind-func print:[void,BTree*]*
  (lambda (tree)
    (printout "<")
    (let ((f (lambda (tree)
               (if (null? tree) void
                   (begin
                     (printout (BTree_value tree) " ")
                     (f (tref tree 1))
                     (f (BTree_right tree))
                     void)))))
      (f tree)
      (printout ">")
      void)))

(bind-func BTree_leaf:[BTree:<!a,BTree*,BTree*>*,!a]*
  (lambda (value)
    (BTree value null null)))

(bind-func BTree_value:[!a,BTree*]*
  (lambda (tree)
    (tref tree 0)))

(bind-func BTree_left:[BTree*,BTree*]*
  (lambda (tree)
    (tref tree 1)))

(bind-func BTree_right:[BTree*,BTree*]*
  (lambda (tree)
    (tref tree 2)))

(bind-func BTree_map:[BTree:<!v,BTree*,BTree*>*,BTree:<!b,BTree*,BTree*>*,[!v,!r]*]*
  (lambda (tree fn)
    (if (null? tree) null
        (BTree (fn (tref tree 0))
               (BTree_map fn (tref tree 1))
               (BTree_map fn (BTree_right tree))))))

(bind-func BTree_walk_preorder:[void,BTree:<!v,BTree*,BTree*>*,[void,!v]*]
  (lambda (tree fn)
    (if (null? tree) null
        (begin
          (fn (tref tree 0))
          (BTree_walk_preorder fn (BTree_left tree))
          (BTree_walk_preorder fn (BTree_right tree))))))

(bind-func BTree_walk_inorder:[void,BTree:<!v,BTree*,BTree*>*,[void,!v]*]
  (lambda (tree fn)
    (if (null? tree) null
        (begin
          (BTree_walk_inorder fn (BTree_left tree))
          (fn (tref tree 0))
          (BTree_walk_inorder fn (BTree_right tree))))))

(bind-func BTree_walk_postorder:[void,BTree:<!v,BTree*,BTree*>*,[void,!v]*]
  (lambda (tree fn)
    (if (null? tree) null
        (begin
          (BTree_walk_postorder fn (BTree_left tree))
          (BTree_walk_postorder fn (BTree_right tree))
          (fn (tref tree 0))))))

;; ;; imperative version
;; (bind-func BTree_walk_preorder:[void,BTree:<!v,BTree*,BTree*>*,[void,!v]*]
;;   (lambda (tree fn)
;;     (let ((to_visit (list)))
;;       (while (not (or (null? to_visit)
;;                       (null? tree)))
;;         (if (not (null? tree))
;;             (begin
;;               ;; call the render callback
;;               (fn (tref tree 0))
;;               ;; set the other children to walk later
;;               (if (not (null? (cdr (tref tree 5))))
;;                   (set! to_visit
;;                         (append (cdr (tref tree 5))
;;                                 to_visit)))
;;               ;; keep walking deeper
;;               (set! tree (car (tref tree 5))))
;;             (begin
;;               ;; go back up and walk a different branch
;;               (set! tree (car to_visit))
;;               (set! to_visit (cdr to_visit))))))))


;; a generic tree

;; (bind-type Tree <!nt,List:<Tree*,List*>*>)

;; (bind-func Tree_walk_preorder:[void,Tree:<!nt,List:<Tree*,List*>*>,[void,!nt]*]
;;   (lambda (tree fn)
;;     (let ((to_visit (list)))
;;       (while (not (or (null? to_visit)
;;                       (null? tree)))
;;         (if (not (null? tree))
;;             (begin
;;               ;; call the render callback
;;               (fn (tref tree 0))
;;               ;; set the other children to walk later
;;               (if (not (null? (cdr (tref tree 5))))
;;                   (set! to_visit
;;                         (append (cdr (tref tree 5))
;;                                 to_visit)))
;;               ;; keep walking deeper
;;               (set! tree (car (tref tree 5))))
;;             (begin
;;               ;; go back up and walk a different branch
;;               (set! tree (car to_visit))
;;               (set! to_visit (cdr to_visit))))))))

;; (bind-func Tree_find:[]
;;   (lambda (tree visit)
;;     ))

;; (bind-func Tree_map:[]
;;   (lambda (tree visit)
;;     ))

;; (bind-func Tree_reduce:[]
;;   (lambda (tree visit)
;;     ))



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;; Some extra string stuff
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(bind-func regex_match_a
  (lambda (regex str)
    (rmatch regex (string_cstr str))))

(bind-func regex_match_b
  (lambda (regex str)
    (rmatch (string_cstr regex) (string_cstr str))))

(bind-poly regex_match regex_match_a)
(bind-poly regex_match regex_match_b)

(bind-func regex_split_a
  (lambda (regex str)
    (let ((a:i8* (salloc 2048))
          (b:i8* (salloc 2048)))
      (sprintf a "")
      (sprintf b "")
      (rsplit regex (string_cstr str) a b)
      (append (list (if (> (strlen a) 0)
                        (Str a) (Str str)))
              (if (> (strlen b) 0)
                  (regex_split_a regex (Str b))
                  null)))))

(bind-func regex_split_b
  (lambda (regex str)
    (let ((a:i8* (salloc 2048))
          (b:i8* (salloc 2048)))
      (sprintf a "")
      (sprintf b "")
      (rsplit (string_cstr regex) (string_cstr str) a b)
      (append (list (if (> (strlen a) 0)
                        (Str a) (Str str)))
              (if (> (strlen b) 0)
                  (regex_split_b regex (Str b))
                  null)))))

(bind-poly regex_split regex_split_a)
(bind-poly regex_split regex_split_b)

(bind-func regex_replace_a
  (lambda (regex str:String* replace:String*)
    (let ((res:i8* (salloc 4096)))
      (rreplace regex
                (string_cstr str)
                (string_cstr replace)
                res)
      (Str res))))

(bind-func regex_replace_b
  (lambda (regex str:String* replace)
    (let ((res:i8* (salloc 4096)))
      (rreplace regex
                (string_cstr str)
                replace
                res)
      (Str res))))

(bind-poly regex_replace regex_replace_a)
(bind-poly regex_replace regex_replace_b)

