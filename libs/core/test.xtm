;;; tests/test.xtm -- testing framework for Extempore

;; Author: 
;; Keywords: extempore
;; Required dylibs: 

;;; Commentary:

;; These functions & macros provide a basic unit testing framework for
;; Extempore - load before running any of the other .xtm files in
;; tests/

;; To test an xtlang function, bind-func it, and then call it inside
;; an xtmtest wrapper, e.g.

;; to only test if if compiles:
;; (xtmtest (test_bit_twiddle_2))

;; to test on an i64 return value (second argument)
;; (xtmtest (test_bit_twiddle_2) 0)

;; To run the tests, M-x eval-buffer (in the buffer with the tests in
;; it, not this one). Once you've run all the xtmtest tests,
;; (xtmtest-print-results) does what it says on the tin.

;;; Code:

(sys:load-preload-check 'test)
(define *xtmlib-test-loaded* #f)

(define *xtmtest-results*
  '((correct)
    (incorrect)
    (compile)
    (no-compile)))

(assoc 'correct *xtmtest-results*)

(define xtmtest-update-test-result
  (lambda (func-sym label expected got) 
    (let ((tail (assoc label *xtmtest-results*))
          (reslist (list func-sym expected got)))
      (set-cdr! tail
                (cons (list func-sym expected got)
                      (cl:remove-if (lambda (res) (equal? res reslist))
                                    (cdr tail)))))
    (print "result: ")
    (print-with-colors (case label
                         ('correct 'green)
                         ('incorrect 'magenta)
                         ('compile 'yellow)
                         ('no-compile ' red))
                       'default #t (print label "\n"))))

(define-macro (xtmtest form call . expected-result)
  `(let ((func-sym (quote ,(car call)))
         (exp-res ,(if (null? expected-result)
                       #f
                       (car expected-result))))
     (print "xtmtest ")
     (print-with-colors 'cyan 'default #t (print func-sym))
     (print "...\n")
     (catch (xtmtest-update-test-result func-sym 'no-compile #f #f)
            (eval ,form (interaction-environment))
            (catch (xtmtest-update-test-result func-sym 'compile #f #f)
                   (let ((result (eval ,call (interaction-environment))))
                     (if (or (not exp-res) (equal? exp-res result))
                         (xtmtest-update-test-result func-sym 'correct exp-res result)
                         (xtmtest-update-test-result func-sym 'incorrect exp-res result)))))))

(define xtmtest-print-results
  (lambda ()
    (println)
    (print-with-colors 'black 'blue #t (print " Test results "))
    (print "\n\n")
    (for-each (lambda (label-list)
                (print-with-colors 'blue 'default #t (print (car label-list) "\n\n"))
                (for-each (lambda (reslist)
                            (print "- ")
                            (print-with-colors (cdr (assoc (car label-list) '((correct . green)
                                                                              (incorrect . yellow)
                                                                              (compile . magenta)
                                                                              (no-compile . red))))
                                               'default #f (print (car reslist)))
                            (cond ((and (equal? (car label-list) 'correct)
                                        (cadr reslist))
                                   (print "   result =" (caddr reslist))
                                   (print ", was expecting") (cadr reslist))
                                  ((equal? (car label-list) 'incorrect)
                                   (print "   result =" (caddr reslist))
                                   (print ", was expecting") (cadr reslist)))
                            (println))
                          (cdr label-list))
                (println))
              *xtmtest-results*)))

(define *xtmlib-test-loaded* #t)
(print-with-colors 'green 'default #f (print " done.\n\n"))
