;;; gui.xtm -- simple 2D GUI library

;; Author: Ben Swift
;; Keywords: extempore
;; Required dylibs: libGL, libglfw3, libnanovg

;;; Commentary:

;; 

;;; Code:

(sys:load "libs/gui.xtm" 'quiet)
(sys:load-preload-check 'gui)
(define *xtmlib-gui-loaded* #f)

(impc:aot:suppress-aot-do
 (sys:load "libs/core/std.xtm")
 (sys:load "libs/external/nanovg.xtm")
 (sys:load "libs/external/glfw3.xtm"))
(impc:aot:insert-sexp '(sys:load "libs/core/std.xtm" 'quiet))
(impc:aot:insert-sexp '(sys:load "libs/external/nanovg.xtm" 'quiet))
(impc:aot:insert-sexp '(sys:load "libs/external/glfw3.xtm" 'quiet))

(impc:aot:insert-header "xtmgui")

;; 0. id
;; 1. size
;; 2. type (flags)
;; 3. callback (to draw widget, handle events)
;; 4. parent widget
;; 5. left child widget
;; 6. right child widget
(bind-type Widget <i32,float,i64,[void,Widget*]*,Widget*,Widget*,Widget*>)

(bind-alias WidgetCB [void,Widget*]*)

;; accessors
(bind-func Widget_id
  (lambda (widget:Widget*)
    (tref widget 0)))

(bind-func Widget_size
  (lambda (widget:Widget*)
    (tref widget 1)))

(bind-func Widget_set_size
  (lambda (widget:Widget* size)
    (tset! widget 1 size)))

(bind-func Widget_type
  (lambda (widget:Widget*)
    (tref widget 2)))

(bind-func Widget_cb
  (lambda (widget:Widget*)
    (tref widget 3)))

(bind-func Widget_set_cb
  (lambda (widget:Widget* cb:WidgetCB)
    (tset! widget 3 cb)))

(bind-func Widget_parent
  (lambda (widget:Widget*)
    (tref widget 4)))

(bind-func Widget_set_parent
  (lambda (child:Widget* parent)
    (tset! child 4 parent)))

(bind-func Widget_left_child
  (lambda (widget:Widget*)
    (tref widget 5)))

(bind-func Widget_set_left_child
  (lambda (parent:Widget* child)
    (tset! parent 5 child)))

(bind-func Widget_right_child
  (lambda (widget:Widget*)
    (tref widget 6)))

(bind-func Widget_set_right_child
  (lambda (parent:Widget* child)
    (tset! parent 6 child)))

(bind-func Widget_print
  (lambda (widget:Widget*)
    (if (null? widget)
        (printf "<Widget 0x0>")
        (printf "<Widget %p\n        id=%d\n        size=%.3f\n        type=%lld\n        parent=%p\n        Lchild=%p\n        Rchild=%p\n>"
                widget
                (Widget_id widget)
                (convert (Widget_size widget) double)
                (Widget_type widget)
                ;; (Widget_cb widget) ;; callback
                (Widget_parent widget)
                (Widget_left_child widget)
                (Widget_right_child widget)))
    void))

(bind-func Widget_tostring
  (lambda (widget:Widget*)
    (let ((s:i8* (salloc 1024)))
      (if (null? widget)
          (sprintf s "<Widget 0x0>")
          (sprintf s
                   "<Widget %p\n        id=%d\n        size=%.3f\n        type=%lld\n        parent=%p\n        Lchild=%p\n        Rchild=%p\n>"
                   widget
                   (Widget_id widget)
                   (convert (Widget_size widget) double)
                   (Widget_type widget)
                   ;; (Widget_cb widget) ;; callback
                   (Widget_parent widget)
                   (Widget_left_child widget)
                   (Widget_right_child widget)))
      (Str s))))

(bind-poly print Widget_print)
(bind-poly tostring Widget_tostring)

;; 0. GLFW window
;; 1. nanovg context
;; 2. root widget
;; 3. width
;; 4. height
;; 5. pixel ratio
(bind-type GUI <GLFWwindow*,NVGcontext*,Widget*,float,float,float>)

;; accessors
(bind-func GUI_window
  (lambda (gui:GUI*)
    (tref gui 0)))

(bind-func GUI_context
  (lambda (gui:GUI*)
    (tref gui 1)))

(bind-func GUI_root
  (lambda (gui:GUI*)
    (tref gui 2)))

(bind-func GUI_set_root
  (lambda (gui:GUI* widget)
    (tset! gui 2 widget)))

(bind-func GUI_width
  (lambda (gui:GUI*)
    (tref gui 3)))

(bind-func GUI_height
  (lambda (gui:GUI*)
    (tref gui 4)))

(bind-func GUI_pixel_ratio
  (lambda (gui:GUI*)
    (tref gui 5)))

(bind-func GUI_print
  (lambda (gui:GUI*)
    (if (null? gui)
        (printf "<GUI 0x0>")
        (printf "<GUI %p\n     window=%p\n     context=%p\n     root=%p\n     width=%.1f\n     height=%.1f\n     pixelRatio=%.1f\n>"
                gui
                (GUI_window gui)
                (GUI_context gui)
                (GUI_root gui)
                (convert (GUI_width gui) double)
                (convert (GUI_height gui) double)
                (convert (GUI_pixel_ratio gui) double)))
    void))

(bind-func GUI_tostring
  (lambda (gui:GUI*)
    (let ((s:i8* (salloc 1024)))
      (if (null? gui)
          (sprintf s "<GUI 0x0>")
          (sprintf s
                   "<GUI %p\n     window=%p\n     context=%p\n     root=%p\n     width=%.1f\n     height=%.1f\n     pixelRatio=%.1f\n>"
                   gui
                   (GUI_window gui)
                   (GUI_context gui)
                   (GUI_root gui)
                   (convert (GUI_width gui) double)
                   (convert (GUI_height gui) double)
                   (convert (GUI_pixel_ratio gui) double)))
      (Str s))))

(bind-poly print GUI_print)
(bind-poly tostring GUI_tostring)

;; this is the global gui structure - there can only be one in each
;; Extempore process

(bind-val EXTEMPORE_GUI GUI*)

(bind-func gui_init
  (lambda (width height)
    ;; (if (not (null? EXTEMPORE_GUI))
    ;;     (begin
    ;;       (println "Extempore GUI is already initialised, and cannot be re-initialised.")
    ;;       EXTEMPORE_GUI))
    (let ((ww:i32* (alloc))
          (wh:i32* (alloc))
          (fbw:i32* (alloc))
          (fbh:i32* (alloc))
          (window null)
          (vg null))
      ;; set up GLFW
      (glfwSetErrorCallback (convert (get_native_fptr glfw_error_callback)))
      (set! window (glfw_init_and_create_interaction_window width height))
      ;; (glfwSetInputMode window GLFW_STICKY_KEYS 1)
      ;; (glfwSetInputMode window GLFW_STICKY_MOUSE_BUTTONS 1)
      ;; set up nanovg
      (set! vg (nvg_create_context))
      (glfwGetWindowSize window ww wh)
      (glfwGetFramebufferSize window fbw fbh)
      ;; create the new GUI data type
      (set! EXTEMPORE_GUI
            (GUI_h
             window
             vg
             null
             (convert (pref ww 0) float)
             (convert (pref wh 0) float)
             (/ (convert (pref fbw 0) float) (convert (pref ww 0) float)))))))

;; walks the tree in pre-order
(bind-func gui_render_subtree
  (lambda (widget:Widget* x:float y:float w:float h:float)
    (if (null? widget)
        widget
        (if (and (null? (Widget_left_child widget))
                 (null? (Widget_right_child widget)))
            ;; if a leaf node, set the nanovg transform matrix and
            ;; call the render callback
            (let ((winWidth (GUI_width EXTEMPORE_GUI))
                  (winHeight (GUI_height EXTEMPORE_GUI))
                  (vg (GUI_context EXTEMPORE_GUI)))
              (nvgResetTransform vg)
              (nvgTranslate vg x y)
              (nvgScale vg w h)
              ((Widget_cb widget) widget)
              null)
            ;; otherwise recurse into the child widgets - horizontal
            ;; splits only at this point
            (let ((lchild (Widget_left_child widget))
                  (rchild (Widget_right_child widget))
                  (lsize (Widget_size lchild))
                  (rsize (Widget_size rchild))
                  (totalsize (+ lsize rsize)))
              ;; normalise sizes
              (set! lsize (/ lsize totalsize))
              (set! rsize (/ rsize totalsize))
              ;; recurse into children
              (gui_render_subtree (Widget_left_child widget)
                                  x
                                  y
                                  w
                                  (* lsize h))
              (gui_render_subtree (Widget_right_child widget)
                                  x
                                  (+ y (* rsize h))
                                  w
                                  (* rsize h))
              (convert null))))))

;; for debugging
(bind-func draw_rect
  (lambda ()
    void))

(bind-func gui_render
  (lambda ()
    (glClear (bitwise-or GL_COLOR_BUFFER_BIT
                         GL_DEPTH_BUFFER_BIT
                         GL_STENCIL_BUFFER_BIT))
    (nvgBeginFrame (GUI_context EXTEMPORE_GUI)
                   (convert (GUI_width EXTEMPORE_GUI))
                   (convert (GUI_height EXTEMPORE_GUI))
                   (GUI_pixel_ratio EXTEMPORE_GUI))
    (gui_render_subtree (GUI_root EXTEMPORE_GUI) 0. 0. (GUI_width EXTEMPORE_GUI) (GUI_height EXTEMPORE_GUI))
    (nvgEndFrame (GUI_context EXTEMPORE_GUI))
    (glfwPollEvents)
    (glfwSwapBuffers (GUI_window EXTEMPORE_GUI))))

(bind-func Widget_leaf_p
  (lambda (widget:Widget*)
    (and (null? (Widget_left_child widget))
         (null? (Widget_right_child widget)))))

(bind-func Widget_inner_p
  (lambda (widget:Widget*)
    (not (Widget_leaf_p widget))))

(bind-func Widget_parent_p
  (lambda (child:Widget* parent:Widget*)
    (= (Widget_parent child) parent)))

(bind-func Widget_child_p
  (lambda (parent:Widget* child:Widget*)
    (or (= (Widget_left_child parent) child)
        (= (Widget_right_child parent) child))))

;; walk the widget tree (preorder), applying fn to each widget
(bind-func Widget_walk_preorder
  (lambda (widget:Widget* visit_fn:[void,Widget*]*)
    (if (null? widget)
        widget
        (begin
          (visit_fn widget)
          (Widget_walk_preorder (Widget_left_child widget) visit_fn)
          (Widget_walk_preorder (Widget_right_child widget) visit_fn)
          null))))

;; returns the first widget for which pred returns true
(bind-func Widget_find_preorder
  (lambda (widget:Widget* pred:[i1,Widget*]*)
    (if (and (not (null? widget)) (pred widget))
        widget
        (if (not (null? widget))
            (begin
              (Widget_find_preorder (Widget_left_child widget) pred)
              (Widget_find_preorder (Widget_right_child widget) pred))
            null))))

;; some useful helper search functions

(bind-func Widget_find_parent
  (lambda (widget:Widget*)
    (Widget_find_preorder
     (GUI_root EXTEMPORE_GUI)
     (lambda (w)
       (Widget_parent_p widget w)))))

(bind-func Widget_find_leaf
  (lambda ()
    (Widget_find_preorder
     (GUI_root EXTEMPORE_GUI)
     Widget_leaf_p)))

(bind-func Widget_find_with_id
  (lambda (id)
    (Widget_find_preorder
     (GUI_root EXTEMPORE_GUI)
     (lambda (w)
       (= (Widget_id w) id)))))

(bind-poly Widget_find Widget_find_preorder)
;; (bind-poly Widget_find Widget_find_parent) ;; this is perhaps too confusing
(bind-poly Widget_find Widget_find_with_id)

;; modify the GUI tree

(bind-func Widget_add_left
  (lambda (new_widget:Widget*)
    (if (not (Widget_leaf_p new_widget))
        (begin (println "Cannot add non-leaf widget")
               new_widget)
        (let ((old_leaf (Widget_find_leaf)))
          (if (null? old_leaf)
              (GUI_set_root EXTEMPORE_GUI new_widget)
              (let (;; reverse these two assignments to add the new
                    ;; widget as the left child
                    (new_lchild new_widget)
                    (new_rchild old_leaf)
                    (old_parent (Widget_parent old_leaf))
                    (new_parent (Widget_h -1
                                          (Widget_size old_leaf)
                                          (Widget_type old_leaf)
                                          null
                                          (Widget_parent old_leaf)
                                          new_lchild
                                          new_rchild)))
                ;; set up new parent pointers
                (Widget_set_parent new_lchild new_parent)
                (Widget_set_parent new_rchild new_parent)
                ;; if necessary, reconnect the widget tree
                (if (null? old_parent)
                    ;; if old leaf node was the root node
                    (GUI_set_root EXTEMPORE_GUI new_parent)
                    ;; otherwise set child pointers in old parent
                    (if (= (Widget_left_child old_parent) old_leaf)
                        (Widget_set_left_child old_parent new_parent)
                        (if (= (Widget_right_child old_parent) old_leaf)
                            (Widget_set_right_child old_parent new_parent))
                        (println "could not reconnect widget tree")))
                ;; return the new_lchild
                new_lchild))))))

;; add widgets to left by default
(bind-poly Widget_add Widget_add_left)

(bind-func gui_validate
  (lambda ()
    ;; check that all nodes are either full, or leaves
    (null?
     (Widget_find_preorder
      (GUI_root EXTEMPORE_GUI)
      (lambda (w)
        ;; need an exclusive or...
        (not (or (and (null? (Widget_left_child w))
                      (null? (Widget_right_child w)))
                 (and (not (null? (Widget_left_child w)))
                      (not (null? (Widget_right_child w)))))))))))

;;;;;;;;;;;;;
;; widgets ;;
;;;;;;;;;;;;;

(bind-val WIDGET_BUTTON i64 (real->integer (expt 2 0)))
(bind-val WIDGET_LABEL i64 (real->integer (expt 2 1)))
(bind-val WIDGET_IMAGE i64 (real->integer (expt 2 2)))
(bind-val WIDGET_FBO i64 (real->integer (expt 2 3)))

;; split types

(bind-val WIDGET_SPLIT_VERTICAL i64 (real->integer (expt 2 4)))
(bind-val WIDGET_SPLIT_HORIZONTAL i64 (real->integer (expt 2 5)))

;; create

(bind-func Widget_create_with_cb
  (lambda (id size type cb:WidgetCB)
    (Widget_h id size type cb null null null)))

(bind-poly Widget_create Widget_create_with_cb)

;; destroy (free)

;; this should really fix up the widget tree as well, but that makes
;; it a fair bit more complicated. left as an excercise to the reader :)
(bind-func Widget_destroy
  (lambda (widget:Widget*)
    (free widget)))

;;;;;;;;;;;;;;;;;;;;;;;;;;
;; mouse/keyboard input ;;
;;;;;;;;;;;;;;;;;;;;;;;;;;

;; this stuff is heavily glfw-based



;; button

(bind-func Widget_button_create
  (lambda (label:i8* r:float g:float b:float)
(bind-func Widget_create_button
  (lambda (id size label:i8* r g b)
    (let ((lab (Str label))
          (cb (lambda (widget:Widget*)
                (println lab)
                (nvgSave (GUI_context EXTEMPORE_GUI))
                (nvgBeginPath (GUI_context EXTEMPORE_GUI))
                ;; assume matrix transform -> [0,1]^2 has been applied
                (nvgFillColor (GUI_context EXTEMPORE_GUI) (nvgRGBf r g b))
                (nvgRect (GUI_context EXTEMPORE_GUI) .1 .1 .8 .8)
                (nvgFill (GUI_context EXTEMPORE_GUI))
                (nvgRestore (GUI_context EXTEMPORE_GUI))
                void)))
      (Widget_create id size (bor WIDGET_BUTTON WIDGET_SPLIT_HORIZONTAL) cb))))

(impc:aot:insert-footer "xtmgui")
(define *xtmlib-gui-loaded* #t)
