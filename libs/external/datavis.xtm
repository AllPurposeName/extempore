;;; datavis.xtm -- data visualisation library

;; Author: Ben Swift
;; Keywords: extempore
;; Required dylibs: libnanovg, libglfw

;;; Commentary:

;;

;;; Code:

(sys:load "libs/aot-cache/datavis.xtm" 'quiet)
(sys:load-preload-check 'datavis)
(define *xtmlib-datavis-loaded* #f)

(impc:aot:suppress-aot-do
 (sys:load "libs/core/std.xtm")
 (sys:load "libs/external/gui.xtm"))
(impc:aot:insert-forms
 (sys:load "libs/core/std.xtm" 'quiet)
 (sys:load "libs/external/gui.xtm" 'quiet))

(impc:aot:insert-header "xtmdatavis")

(bind-func dv_draw_axes_2D
  "draw 2D axes"
  (lambda (bounds:Rect*)
    (letz ((vg (GUI_context EXTEMPORE_GUI))
           (padding .1)
           (x (tref bounds 0))
           (y (tref bounds 1))
           (w (tref bounds 2))
           (h (tref bounds 3)))
      (_nvgStrokeColor vg (NVGcolor 1. 1. 1. 1.))
      (nvgBeginPath vg)
      ;; max y (top left)
      (nvgMoveTo vg
                 (+ x (* w padding))
                 (+ y (* h padding)))
      ;; origin
      (nvgLineTo vg
                 (+ x (* w padding))
                 (+ y (* h (- 1. padding))))
      ;; max x (bottom right)
      (nvgLineTo vg
                 (+ x (* w (- 1. padding)))
                 (+ y (* h (- 1. padding))))
      (nvgStroke vg)
      0)))

(bind-func dv_draw_axis_labels_2D
  "draw 2D axis labels"
  (lambda (bounds:Rect* xlabs:float* nxlabs ylabs:float* nylabs)
    (letz ((vg (GUI_context EXTEMPORE_GUI))
           (padding 0.1)
           (x (tref bounds 0))
           (y (tref bounds 1))
           (w (tref bounds 2))
           (h (tref bounds 3))
           (font_size 20.)
           (str:i8* (zalloc 256))
           (i 0))
      (nvgFontSize vg font_size)
      (_nvgFillColor vg (NVGcolor .7 .7 .7 1.))
      (nvgBeginPath vg)
      ;; x labels
      (dotimes (i nxlabs)
        (sprintf str "%.2f" (convert (pref xlabs i) double))
        (nvgText vg
                 (+ (+ x (* w padding))
                    (* (convert i) (/ (* w (- 1. (* 2. padding))) (convert (- nxlabs 1)))))
                 (+ (+ y (* h (- 1. padding))) font_size)
                 str
                 null))
      ;; y labels
      (dotimes (i nylabs)
        (sprintf str "%.2f" (convert (pref ylabs i) double))
        (nvgText vg
                 (+ x font_size)
                 (- (+ y (* h (- 1. padding)))
                    (* (convert i) (/ (* h (- 1. (* 2. padding))) (convert (- nylabs 1)))))
                 str
                 null))
      (nvgFill vg)
      0)))

(bind-func dv_draw_title
  "draw a title (top-center)"
  (lambda (bounds:Rect* title:i8*)
    (letz ((vg (GUI_context EXTEMPORE_GUI))
           ;; (padding 0.1)
           (x (tref bounds 0))
           (y (tref bounds 1))
           (w (tref bounds 2))
           (h (tref bounds 3))
           (font_size 40.)
           (textwidth 0.))
      (nvgFontSize vg font_size)
      (set! textwidth (nvgTextBounds vg 0. 0. title null null))
      (_nvgFillColor vg (NVGcolor 1. 1. 1. 1.))
      (nvgBeginPath vg)
      (nvgText vg
               (+ x (* (- w textwidth) .5))
               (+ y font_size)
               title
               null)
      (nvgFill vg)
      0)))

(bind-func dv_draw_bars
  "draw 2D axis labels"
  (lambda (bounds:Rect* bars:float* nbars)
    (letz ((vg (GUI_context EXTEMPORE_GUI))
           (padding 0.1)
           (x (tref bounds 0))
           (y (tref bounds 1))
           (w (tref bounds 2))
           (h (tref bounds 3))
           (barheight 0.)
           (i 0))
      ;; x labels
      (nvgBeginPath vg)
      (dotimes (i nbars)
        (set! barheight (* (pref bars i) h (- 1. (* 2. padding))))
        (nvgRect vg
                 (+ (+ x (* w padding))
                    (* (convert i) (/ (* w (- 1. (* 2. padding))) (convert nbars))))
                 (+ y (* h padding) (- (* h (- 1. (* 2. padding))) barheight))
                 (/ (* w (- 1. (* 2. padding))) (convert nbars))
                 barheight))
      (_nvgFillColor vg (NVGcolor .2 .2 .9 1.))
      (nvgFill vg)
      (_nvgStrokeColor vg (NVGcolor 0. 0. 0. 1.))
      (nvgStrokeWidth vg 3.)
      (nvgStroke vg)
      0)))

(bind-val GUI_BARPLOT i64 (<< 1 10))
(bind-val GUI_SCATTERPLOT i64 (<< 1 11))
(bind-val GUI_LINEPLOT i64 (<< 1 12))

(impc:aot:insert-footer "xtmdatavis")
(define *xtmlib-datavis-loaded* #t)
