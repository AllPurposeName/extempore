;;; xtmrender1.xtm -- using the Extempore 3D rendering pipeline

;; Author: Andrew Sorensen
;; Keywords: extempore
;; Required dylibs: libGL, libglfw3, libassimp, libsoil

;;; Commentary:

;; the peppino model (and other Extempore "assets") are available from
;; http://extempore.moso.com.au/extras/assets.tgz

;;; Code:

;; path to the model files: if you've put assets in the top-level
;; Extempore folder then this will work, otherwise change it to suit
;; (make sure you include / on the end)
(bind-val model_path i8* "assets/models/")

(sys:load "libs/external/assimp.xtm")
(sys:load "libs/external/glfw3.xtm")

(bind-val window GLFWwindow*)
(bind-val width i32 1024)
(bind-val height i32 768)

;; create a window
(call-as-xtlang
 (glfwSetErrorCallback (convert (get_native_fptr glfw_error_callback)))
 ;; some of the shaders in the graphics pipeline use older glsl
 ;; versions, so we need a "compatibility" graphics context
 (set! window (glfw_init_and_create_compatibility_window width height)))

;; for models
(bind-func render:XTMRENDERCB
  (lambda (frame shader m v p data)
    (rotate_matrix m (* .1 (i64tof frame)) 0.0 1.0 0.0)
    (xtm_draw_model (convert data xtm_node*) shader m v p)
    void))

(bind-func gl_render
  (lambda (model:xtm_node*)
    (xtm_render null render null null (convert model i8*))
    void))

(bind-val peppino xtm_node*)

(bind-func set_peppino
  (lambda ()
    (set! peppino (assimp_load_model model_path "/peppino.dae" aiProcess_Triangulate))))

(set_peppino)

(xtm_render_setup 
 (call-as-xtlang (glfw_get_framebuffer_width window))
 (call-as-xtlang (glfw_get_framebuffer_height window)))

(bind-func gl_draw_loop
  (lambda (time:i64 delta_t:double)
    (gl_render peppino)
    ;; swap buffers
    (glfwPollEvents)
    (glfwSwapBuffers window)
    ;; set next frame callback
    (let ((next_time (+ time (convert (* 44100. delta_t)))))
      (callback next_time gl_draw_loop next_time delta_t))))

;; and to start it running...
(gl_draw_loop (now) (/ 1.0 30.0)) ;; 30fps
