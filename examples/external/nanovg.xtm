;;; nanovg.xtm:examples -- nanovg example

;; Author: Ben Swift
;; Keywords: extempore
;; Required dylibs: libnanovg

;;; Commentary:

;;

;;; Code:

(sys:load "libs/external/glfw3.xtm")
(sys:load "libs/external/nanovg.xtm")

(bind-val window GLFWwindow*)

(call-as-xtlang
 (set! window (glfw_init_and_create_fullscreen_window)))

(bind-val vg NVGcontext*)

(call-as-xtlang
 (set! vg (nvgCreateGL3 (+ NVG_ANTIALIAS NVG_STENCIL_STROKES NVG_DEBUG))))

(call-as-xtlang
 (glEnable GL_STENCIL_TEST))

(call-as-xtlang
 (let ((bits:i32* (salloc)))
   (glGetIntegerv GL_MAX_DEPTH_TEXTURE_SAMPLES bits)
   (println "stencil buffer bit depth: " (pref bits 0))))

(bind-val winWidth i32)
(bind-val winHeight i32)
(bind-val fbWidth i32)
(bind-val fbHeight i32)

(call-as-xtlang
 (let ((w:i32* (alloc))
       (h:i32* (alloc)))
   (glfwGetWindowSize window w h)
   (set! winWidth (pref w 0))
   (set! winHeight (pref h 0))
   (glfwGetFramebufferSize window w h)
   (set! fbWidth (pref w 0))
   (set! fbHeight (pref h 0))
   0))

(bind-func draw_test_rect
  (lambda (x y)
    (nvgBeginPath vg)
    (nvgRect vg x y 520. 430.)
    (nvgFillColor vg (nvgRGBA 128 0 128 128))
    (nvgFill vg)))

(bind-func nanovg_draw
  (lambda ()
    (glViewport 0 0 fbWidth fbHeight)
    (glClear (+ GL_COLOR_BUFFER_BIT
                GL_DEPTH_BUFFER_BIT
                GL_STENCIL_BUFFER_BIT))
    (nvgBeginFrame vg
                   (convert fbWidth) (convert fbHeight)
                   (/ (convert fbWidth float)
                      (convert winWidth float)))
    (draw_test_rect 300. 300.)
    (nvgEndFrame vg)
    (glfwSwapBuffers window)))

(define gl-loop
  (lambda (time delta-t)
    (let ((late-by (- (now) time))
          (next-frame-time (+ time (* *second* delta-t))))
      (if (> late-by 0)
          (print "Late by " (* 1.0 (/ late-by *second*)) "seconds\n")
          (nanovg_draw))
      (callback (* 0.9 next-frame-time)
                'gl-loop
                next-frame-time
                delta-t))))

(gl-loop (now) 1)
