(sys:load "libs/external/glfw3.xtm")

(bind-val window GLFWwindow*
  (glfw_init_and_create_window 640 480))

(bind-val fullscreen_quad_vert_data float* 8)

(bind-val vbo VBO*)
(bind-val vao VAO*)
(bind-val sp ShaderProgram)

;; init
(call-as-xtlang
 (pfill! fullscreen_quad_vert_data
         ;; pos (2D)
         -1.0  1.0
          1.0  1.0
         -1.0 -1.0
          1.0 -1.0)
 (set! vbo (VBO_create fullscreen_quad_vert_data 16))
 (set! vao (VAO_create))
 (VAO_bind_attribute vao vbo 0 2 2 0)
 void)

(bind-func gl_draw
  (lambda ()
    (glClear GL_COLOR_BUFFER_BIT)
    (let ((resloc (glGetUniformLocation sp "iResolution"))
          (timeloc (glGetUniformLocation sp "iGlobalTime"))
          (rateloc (glGetUniformLocation sp "iSampleRate")))
      (glUseProgram sp)
      (glUniform3f resloc 1920.0 1080.0 1.0)
      (glUniform1f timeloc (convert (% (clock_clock) 441000.)))
      (glUniform1f rateloc 44100.0))
    (VAO_bind_and_draw_arrays vao GL_TRIANGLE_STRIP 0 4)
    (glfwSwapBuffers window)
    void))

(bind-func create_shadertoy_shader
  (lambda (vsource:i8* fsource:i8*)
    (let ((res (ShaderProgram_create vsource fsource)))
      (if (not (< res 0))
          (set! sp res)))))

(create_shadertoy_shader (file->string "examples/external/shader-tutorials/shadertoy.vert")
                         (file->string "examples/external/shader-tutorials/shadertoy.frag"))

(define shadertoy-loop
  (lambda (time delta-t)
    (gl_draw)
    (let ((next-time (max (+ time (* *second* delta-t)) (now))))
      (callback next-time
                'shadertoy-loop
                next-time 
                delta-t))))

(shadertoy-loop (now) 1/30)

;; now we can swap in a different shader

(create_shadertoy_shader (file->string "examples/external/shader-tutorials/shadertoy.vert")
                         (file->string "examples/external/shader-tutorials/shadertoy-flame.frag"))
