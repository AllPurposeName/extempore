(sys:load "libs/external/shaders-v2.xtm")

(bind-val window GLFWwindow*)

(call-as-xtlang
 (glfw_init)
 (glfw_set_core_profile)
 (set! window (glfw_create_fullscreen_window))
 (glfwMakeContextCurrent window)
 (gl_print_driver_info))

(call-as-xtlang
 (glEnable GL_PROGRAM_POINT_SIZE))

(bind-val point_data float* 15)

(bind-val vbo VBO*)
(bind-val vao VAO*)
(bind-val sp ShaderProgram*)

;; init
(call-as-xtlang
 (pfill! point_data
         ;; pos      colour
          0.0  0.5   1.0 0.0 0.0
          0.5 -0.5   0.0 1.0 0.0
         -0.5 -0.5   0.0 0.0 1.0)
 (set! vbo (create_vbo point_data 15))
 (set! vao (create_vao))
 (bind_attribute vao vbo 0 2 5 0) ;; position
 (bind_attribute vao vbo 1 3 5 2) ;; colour
 void)

(bind-func create_points_shader
  (lambda (vsource:i8* fsource:i8*)
    (set! sp (ShaderProgram_create vsource fsource))))

(create_points_shader (file->string "examples/external/shader-tutorials/points.vert")
                      (file->string "examples/external/shader-tutorials/points.frag"))

(bind-func gl_draw
  (lambda ()
    (glClear GL_COLOR_BUFFER_BIT)
    (glUseProgram (ShaderProgram_id sp))
    (draw_vertex_array vao GL_POINTS 0 3)
    (glfwSwapBuffers window)
    void))

;; draw the points
(gl_draw)
