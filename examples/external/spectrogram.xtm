;;; openvg.xtm -- OpenVG drawing example

;; Author: Andrew Sorensen, Ben Swift
;; Keywords: extempore
;; Required dylibs: libOpenVG

;;; Commentary:

;;

;;; Code:

(sys:load "libs/core/instruments.xtm")
(sys:load "libs/core/pc_ivl.xtm")
(sys:load "libs/external/audio_dsp_ext.xtm")

(bind-instrument fmsynth fmsynth_note_c fmsynth_fx)

(bind-func dsp:DSP
  (let ((F (stft_st_c 64 512 512))
        (left:ComplexS* (F.left))
        (right:ComplexS* (F.right)))
    (lambda (in time chan dat)
      (cond ((< chan 2)
             (F chan
                (+ (* 1.0 (fmsynth in time chan dat))
                   0.0)))
            (else 0.0)))))

(dsp:set! dsp)

(define fullscreen #f)
;; (define width 1920.0)
;; (define height 1200.0)
(define width 1024.0)
(define height 768.0)
(bind-val width float width)
(bind-val height float height)

;; setup opengl
(define *gl-ctx* (gl:make-ctx ":0.0" fullscreen 0.0 0.0 width height))

;; first try to load from prebuilt stdlib
;; (sys:load "libs/xtm.xtm")
;; failing that compile on-the-fly
(sys:load "libs/external/graphics-pipeline.xtm")
(sys:load "libs/external/openvg.xtm")

;; init xtm render pipeline
(xtm_render_setup width height)
(if (<> (xtm-vg-create-context width height) 1) (xtm_vg_print_error))

(bind-func post:XTMRENDERCB
  (let ((path (xtm_create_path))
        (paint (vgCreatePaint))
        (colour:|4,float|* (alloc))
        (dl:Complexf* (dsp.left))
        (dr:Complexf* (dsp.left))
        (t 0)
        (clear_color:xtm_color* (alloc)))
    (vguRect path 0. 0. 3.0 1.0)
    (lambda (frame shader m v p data)
      (let ((i:float 0.0)
            (inc (/ width 200.)))
        (vgLoadIdentity)        
        (afill! colour 0. 0. 0. 1.0)        
        (xtm_vg_clear_rect 0 0 (convert width) (convert height) colour)
        (dotimes (i 200.0)
          (vgLoadIdentity)
          (vgScale 1.0 (* 30. (magC (pref dl (ftoi64 i)))))
          (vgTranslate (convert (* inc i)) 0.0)
          (afill! colour 0. 1. 0. 0.5) 
          (xtm_paint_set_fill paint colour)          
          (vgDrawPath path VG_FILL_PATH)
          (vgLoadIdentity)
          (vgTranslate (convert (* inc i)) height) ;height) ;0.0)          
          (vgScale 1.0 (* -30. (magC (pref dr (ftoi64 i)))))
          (afill! colour 1. 0. 0. 0.5) 
          (xtm_paint_set_fill paint colour)          
          (vgDrawPath path VG_FILL_PATH))
        (xtm_vg_print_error)
        void))))

(bind-func gl_render
  (lambda ()
    (xtm_render null null null post null)
    void))

(define gl-loop
  (lambda (time delta-t)
    (let ((late-by (- (now) time))
          (next-frame-time (+ time (* *second* delta-t))))
      (if (> late-by 0)
          (print "Late by " (* 1.0 (/ late-by *second*)) "seconds\n")
          (begin
            (gl_render)
            (gl:swap-buffers *gl-ctx*)))
      (callback (* 0.9 next-frame-time)
                'gl-loop
                next-frame-time
                delta-t))))

;; start gl loop
(gl-loop (now) 1/30)

;; now sound stuff

(define root 0)

(define l2
  (lambda (beat pitch dur)
    (if (> pitch 100) (set! dur 4.))
    (play fmsynth pitch 100 .2 .5 10.)
    (callback (*metro* (+ beat (* .5 dur))) 'l2 (+ beat dur)
              (if (> pitch 100) 60 (pc:relative pitch 1 (pc:scale root 'aeolian)))
              1/6)))

;; start audio loop
(l2 (*metro* 'get-beat 4) 60 1/6)
