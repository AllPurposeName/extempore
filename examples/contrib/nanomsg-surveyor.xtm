(sys:load "libs/external/nanomsg.xtm")

(bind-val SURVEYOR_ADDRESS i8* "tcp://127.0.0.1:7150")
(bind-val SURVEYOR_SOCKET i32
  (begin (nnsock_create NN_SURVEYOR)
         (nnsock_bind SURVEYOR_SOCKET SURVEYOR_ADDRESS)))

(bind-func nonblocking_read
  (let ((buflen:i64 1024)
        (buf:i8* (zalloc buflen))
        (continue #t))
    (lambda (sock cb:[void,i8*,i32]*)
      (if (< sock 0)
          (nn_println_strerror)
          (let ((nbytes (nn_recv sock buf buflen NN_DONTWAIT)))
            ;; if we get some bytes, trigger the callback
            (if (> nbytes 0)
                (cb buf nbytes))))
      (if continue
          (callback (+ (now) 1000) nonblocking_read sock cb)
          (begin
            (println "stopping nonblocking_read loop")
            0)))))

(bind-func send_survey
  (lambda (sock msg)
    (let ((msglen (+ (strlen msg) 1))
          (bytes (nn_send sock msg msglen 0)))
      (if (or (< bytes 0) (< bytes (convert msglen)))
          (nn_println_strerror)
          void))))

(bind-func print_response
  (lambda (buf:i8* nbytes:i32)
    (begin (printf "recieved response: %.*s\n" nbytes buf) void)))

;; start the read loop running
(call-as-xtlang
 (nonblocking_read SURVEYOR_SOCKET print_response))

(call-as-xtlang (println "SURVEYOR_SOCKET:" SURVEYOR_SOCKET))

;; try it out
(send_survey 0 (string-append "test message" (number->string (random 100))))

;; kill everything
(call-as-xtlang
 (nonblocking_read.continue #f)
 (nn_shutdown SURVEYOR_SOCKET))
